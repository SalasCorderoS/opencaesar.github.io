---
layout: project
title: "OML v2"
author: "Maged Elaasar"
author_image: maged.png
tag: [OML]
categories: [IMCE]
current: false
image:
  feature: oml.png
  credit: Jet Propulsion Laboratory
---

## Summary

The openCAESAR team has released a major revision (v2) of OML that includes:

#### Syntax Simplifications

Changes to the OML abstract syntax and its Java API that does not impact the textual syntax:

1. Merged (and removed) class `AnnotatedElement` into class `IdentifiedElement` (the impact is that `Import` statements can no longer be annotated).
1. Collapsed the `Import` class  hierarchy into a single `Import` class with an enumeration kind that has literals: `extends`, `uses`, and `includes`.
1. Renamed class `Feature` to `Property`, made the latter the supertype of classes `AnnotatedProperty` and `SemanticProperty`, and made class `Relation` extend from `SemanticProperty`.
1. Collapsed the `Restriction` class hierarchy into 3 subclasses restricting range, cardinality, or value of a property, and made class `Entity` has a single collection for restrictions.
1. Removed class `LinkAssertion` (replaced by `PropertyValueAssertion`) and made class `NamedInstance` has a single collection of property value assertions.
1. Moved the types collection (now of concrete type `TypeAssertion`) from both classes `ConceptInstance` and `RelationInstance` into their superclass `NamedInstance`.
1. Made class `RelationEntityPredicate` a subclass of both `UniaryPredicate` and `BinaryPredicate`.
1. Removed the `Reference` class hierarchy entirely and merged it into the `Member` class hierarchy (a member can have either a name or a ref to another member).

---

#### Syntax Changes

Changes to the OML abstract syntax and its Java API that also impacts the textual syntax:


1. The specialization symbol `:>` has changed into `<` to better align with the subset symbol (a subclass is a subset of its super class).

1. The vocabulary term's syntax changed from the old (general) syntax of:

    ```
    Term ::= <keyword> NAME (`<` Specializations)? (`[` Details? Restrictions? `]`)?
    Specializations ::= REF (`,` REF)*
    ```

    to the new (general) syntax of:

    ```
    Term ::= <keyword> NAME (`[` Details `]`)? (`<` Specializations)?
    Specializations ::= (REF (`,` REF)*)? (`[` Restriction*`]`)?
    ```

    For example:
    ```
    concept A < B, C

    concept D [
      key p1
    ] < E, F

    concept G < [
      restricts all scalar property p1 to H
    ] 

    aspect K < M [
      restricts scalar property p1 to "abc"
    ]

    scalar property p1 [
      domain C
      range xsd:string
    ] < p2

    relation entity A [
      from X 
      to Y
      forward a
      functional
    ] < B [
      restricts scalar property p1 to "abc"
    ]
    ```
    
    > Note: The reason for splitting details from restrictions will become clearer once the new feature of equivalence axioms is introduced below.


1. The enumerated instances of a `Concept` can now be specified with the keyword `oneOf` instead of `enumerates`. For example:

    ```
    concept RockyPlanets [
      oneOf Earth, Mars, Venus, Mercury // these are concept instances
    ]
    ```

1. Both `FacetedScalar` and `EnumeratedScalar ` were eliminated and merged into a (now concrete) `Scalar` class. The enumerated literals can be specified as another facet called `oneOf` (along with the other facets like `minLength`). 

    For example:
    ```
    scalar RGBColor <: xsd:string [
      oneOf "Red", "Green", "Blue"
    ]
    ```

1. A concept instance is now defined with the intuitive keyword `instance` instead of the keyword `ci`. It can now be typed by concepts or aspects (instead of concepts only before).

1. A relation instance is now defined with the intuitive keyword `relation instance` instead of the keyword `ri`. It can now be typed by relation entities or aspects (instead of relation entities only before).

1. Changed the `^` symbol between rule predicates to `&` to closely reflect the AND semantics.

---

#### New Features

New features to add to the expressivity of OML abstract syntax (and textual syntax):

1. Made `Annotation` have either a literal value or a reference (to a member) value. This would allow saying for ex.:
    ```
    @rdfs:label "Aggregates"
    @rdfs:seeAlso base:Contains
    relation entity Aggregtes
    ```
1. Added a new class `UnreifiedRelation` as a subclass of `Relation` and (unlike classes `Forward` and `Reverse`) it can be declared independently of a `RelationEntity`. This would allow defining for ex.:   
    ```
    relation performs [ // this is an unreified relation
      from Component 
      to Function 
      reverse isPerformedBy
      functional
    ]
    ```
1. Added a new class `BuiltIn` to represent a builtin function that can be called from a `Rule` as a predicate. Also, added the standard vocabulary of SWRL builtins to the `core-vocabularies` set. This should allow saying for ex. 
    ```
    rule FullName [
          firstName(?x, ?first) & 
          lastName(?x, ?last) &
          swrlb:stringConcat(?full, ?first, " ", ?last) 
          -> fullName(?x, ?full)
    ]
    ```

1. Added the Ability to express `Equivalence` axioms in addition to `Specialization` axioms on vocabulary terms (classifiers and properties).

   The general syntax is:
    ```
    Term ::= <keyword> NAME (`[` Details `]`)? (`<` Specializations)? (`=` Equivalences)?
    Specializations ::= (REF (`,` REF)*)? (`[` Restriction*`]`)?
    Equivalences ::= Equivalence (`,` Equivalance)*
    ```
    
    In the case of classifiers, an `Equivalence` axiom is specified with the syntax:
    ```
    Equivalence ::= (REF (`&` REF)*)? (`[` Restriction*`]`) 
    ```
    
    For example:
    ```
    concept SpicyPizza = Pizza [
      restricts some hasTopping to SpicyTopping
    ]

    aspect A < X = B & C & D [
        restricts scalar property p1 to max 1
        restricts scalar property p2 to exatly 5 P
    ]

    concept Z [
      key p1
    ] < Y, F = M & N

    concept S = M & N, V & W
    ```

    In the case of properties, an `Equivalence` axiom is specified with the syntax:
    ```
    Equivalence ::= REF 
    ```

    For example:
    ```
    relation hasAunt [
      from Person
      to Woman
    ] = hasSisterOfParent

    scalar property p1 [
      domain D
      range xsd:string
    ] < p2 = p3, p4
    ```

    > Note: how the `[Restrictions]` clause can now be specified on both specialization and equvivalence axioms. Also, since there can be more thann one equivalence axioms, each can have its own `[Restrictions]` clause. This is why we separated `[Restrictions]` from `[Details]` (specified only once).



Please provide your feedback to [Maged Elaasar](https://opencaesar.github.io/contributors/Maged%20Elaasar.html) directly, or by commenting on this community [discussion](https://github.com/opencaesar/community/discussions/2).