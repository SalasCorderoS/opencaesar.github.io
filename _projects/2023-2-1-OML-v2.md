---
layout: project
title: "OML v2"
author: "Maged Elaasar"
author_image: maged.png
tag: [OML]
categories: [IMCE]
current: false
image:
  feature: oml.png
  credit: Jet Propulsion Laboratory
---

## Summary

The openCAESAR project has released a v2 of OML, which has affected its textual syntax, graphical notation, and API. In this post, we highlight the notable changes from v1:

#### Textual Syntax Changes

1. In ontologies, whereas in the past, only a subset of details can be specified on a `ref` to an member, now the full details (in most cases) can. This widens significantly the ability to split statements about members in different ontologies (which better supports federation and extensibility). This also means that (most of) the previously **required** details of members have been relaxed as **optional**.

   For example:
   ```
   ref relation entity R1 [
      from C1 // this adds another type as a source of R1
   ]

   ref scalar property name [
      domain Pet // this adds Pet as another domain of name
      functional // this adds the functional flag
   ]
   ```

1. In ontologies, an annotation have now specify a reference (to a member) value in addition to a literal value (only allowed before). 

   For example:
   ```
   @rdfs:label "Aggregates" // literal
   @rdfs:seeAlso base:Contains // reference
   relation entity Aggregtes
   ```

1. In vocabularies, the specialization symbol `:>` has changed into `<` to better align with the semantics that a subclass is a subset of its superclass.

1. In vocabularies, it has been clarified that `restricts` statements, which used to be placed with other term's details between `[]`,  belong instead with the specialization axiom (`<`). This has caused the general term syntax to change from:
   ```
    Term ::= <keyword> NAME (`<` Specializations)? (`[` Details? Restrictions? `]`)?
    Specializations ::= REF (`,` REF)*
   ```

  to:
   ```
    Term ::= <keyword> NAME (`[` Details `]`)? (`<` Specializations)?
    Specializations ::= (REF (`,` REF)*)? (`[` Restriction*`]`)?
   ```
  
  For example:
  ```
  aspect A1 [
    key id // detail
  ] < A2

  concept C1 [
    oneOf c11, c12, c13 // detail
  ] < C2, C3 [
    restricts all scalar property p1 to "abc"
  ]

  concept C2 < C3, C4, C5 [
    restricts all scalar property p1 to "abc"
    restricts scalar property p2 to 2.0
  ]

  relation entity R1 [ 
    from C1     // detail
    to A1       // detail
    forward r1  // detail
  ] < R2 [
    restricts some scalar property p2 to 2.0
  ]

  scalar S1 [
    oneOf "abc", "xyz" // detail
  ] < S2

  scalar property p1 [
    domain D1   // detail
    range R1    // detail
  ] < p2
  ```

1. In vocabularies, a new equivalence axiom (`=`) has been added to a term's syntax in addition to the specialization axiom (`<`). While a specialization axiom says that a term is a sub of another term, an equivalence axiom says that a term is both a sub and a super of another term. The general term's syntax is now:
   ```
   Term ::= <keyword> NAME (`[` Details `]`)? (`<` Specializations)? (`=` Equivalences)?
   Specializations ::= (REF (`,` REF)*)? (`[` Restriction*`]`)?
   Equivalences ::= Equivalence (`,` Equivalance)*
   ```
   
   In the case of classifiers, a single equivalence axiom has the syntax:
   ```
   Equivalence ::= (REF (`&` REF)*)? (`[` Restriction*`]`)  // equals the intersection of other named terms and restrictions
   ```
   
   In the case of scalars and properties, a single equivalence axiom has the syntax:
   ```
   Equivalence ::= REF // equals another named term
   ```

   For example:
   ```
   // A pizza that has a spicy topping is classified as a spicy pizza
   concept SpicyPizza = Pizza [
      restricts some hasTopping to SpicyTopping
   ]
   // A woman that is married is classified as a married woman
   aspect MarriedWoman = Married & Woman
   // A string that has a particular pattern is an SSN
   scalar ssn = xsd:string [
     pattern "[0-9]{3}-[0-9]{2}-[0-9]{4}"
   ]
   // Saying food is spicy is the same as saying that it is hot and vice versa
   scalar property isSpicy [
      domain Food
      range xsd:boolean
      functional
   ] = isHot
   ```

1. In vocabularies, an enumeration for a concept or a scalar can now be specified consistently with a `oneOf` statement in the details between `[]`. This also removed the distiction between enumerated vs faceted scalars (it's only a scalar now)).

   For scalars, the syntax has (partially) changed from:
   ```
   EnumeratedScalar ::= enumerated scalar NAME [
     Literal (, Literal)*
   ]
   ```
   to
   ```
   Scalar ::= scalar NAME [
     (oneOf Literal (, Literal)*)?
     // other details ...
   ]
   ```

   For concepts, the syntax has (partially) changed from:
   ```
   Concept ::= concept NAME [
     (enumerates REF (, REF)*)?
     // other details ...
   ]
   ```
   to
   ```
   Concept ::= concept NAME [
     (oneOf REF (, REF)*)?
     // other details ...
   ]
   ```

   For example:
   ```
   scalar RGB [
     oneOf "Red", "Green", "Blue" // literals
   ]

   concept RockyPlanet [
     oneOf Mercury, Venus, Earth, Mars // instances
   ]
   ```

1. In vocabularies, it has been clarified that facets (e.g., length, pattern, etc.) of a scalar do not belong to its details between `[]`, but rather to the scalar equivalence axiom. This allows such facets to be specified on a per equivalence axiom basis.

   For example:
   ```
   scalar ssn = xsd:string [
     pattern "[0-9]{3}-[0-9]{2}-[0-9]{4}"
   ]

   scalar positiveReal = xsd:real [
     minInclusive 0
   ], xsd:real [
     maxInclusive 9999
   ]
   ```

1. In vocabularies, a new unreified relation can now be specified as an alternative to a (refieid) relation entity. This supports cases where instances of the relation are not anticipated (and to avoid synthesizing them when mapping vocabularies from other formats that do not natively support reified relations). The syntax of an unreified relation resembles closely that of a relation entity (except the `forward` keyword). A (partial) syntax of an unreieifed relation is as follows:
   ```
   Relation ::= relation NAME ([
    from REF
    to REF
    (reverse NAME)? // notice there is no forward NAME
    // other details in a relation entity
   ])? // specialization and equivalence axioms to other relations
   ```
   
   For example:
   ```
   relation isHusbandOf [
     from Man
     to Woman
     reverse isWifeOf
   ]

   relation entity IsTeacherOf [
    from Teacher
    to Student
    forward isTeacherOf
    reverse isStudentOf
   ]
   ```

1. In vocabularies, an unreified relation can be referenced anywhere other relations (forward, reverse) can. This includes restriction statements (`restricts relation REL_REF`), specialization statements (`< REL_REF`), equivalence statements (`= REL_REF`), rule predicates (`REL_REF(ARG1, ARG2)`), assertion axioms (`REL_REF TARGET_REF`), etc. Notice that relations are properties, whereas relation entities are types. Therefore, a relation cannot be a supertype of a relation enity (and vice versa). But, an unreified relation can have a forward or reverse relation as a supertype.

1. In vocabularies, the `from` and `to` keywords of relations, as well as the `domain` and `range` keywords of properties, can now specify more than one type as a value (additional types can be specified on a `ref`). The semantics of this situation is an intersection of those multiple types. This makes a value asserted to be a subject (or object) of a property (or relation) that has multiple domains (or ranges) classified by the intersection (not the union) of those types.

   For example:
   ```
   // In a vocabulary
   relation isBoxedIn [
      from Red, Ball
      to Box
   ]
   relation isPetOf [
      from Cat, Dog
      to Person
   ]
   // In a description
   instance Ball1 [
     isBoxedIn Box1 // this will cause Ball1 to be classified by both Red and Ball
   ]
   instance Tom [
      isPetOf Jack // this will cause Tom to be classified by both Cat and Dog (which could be disjoined leding to a contradiction)
   ]
   ```

1. In vocabularies, the symbol `^` separating rule predicates has changed to `&` (to better reflect the AND semantics).

1. In vocabularies, a new term `builtIn` can be used to define a builtin function that can be referenced by a rule antecedant predicate. A standard set of SWRL builtins has been added to the `core-vocabularies` library. Note that a custom `builtIn` cannot be specified yet.
    ```
    // in the <ttp://www.w3.org/2003/11/swrlb#> vocabulary
    @rdfs:comment "Satisfied iff the first argument is equal to the string resulting from the concatenation of the strings the second argument through the last argument."
    builtin stringConcat

    // in a vocabulary
    rule FullName [
          firstName(?x, ?first) & 
          lastName(?x, ?last) &
          swrlb:stringConcat(?full, ?first, " ", ?last) // builtins can only be referenced in antecedent
          -> fullName(?x, ?full)
    ]

1. In descriptions, the symbol `ci` for a concept instance has now changed to the more readable `instance`. Such instance can also now be typed by both concepts and aspects (whereas only concepts were valid before).

1. In descriptions, the symbol `ri` for a relation instance has now changed to the more readable `relation instance`. Such instance can also now be typed by both relation entities and aspects (whereas only relation entities were valid before).

#### API Changes

The above changes to the textual syntax has induced changes to the Java API. Other changes were done too to simplify the APIs.

1. Merged (and removed) class `AnnotatedElement` into class `IdentifiedElement` (the impact is that `Import` statements can no longer be annotated).
1. Collapsed the `Import` class  hierarchy into a single `Import` class with an enumeration kind that has literals: `extends`, `uses`, and `includes`.
1. Renamed class `Feature` to `Property`, made the latter the supertype of classes `AnnotatedProperty` and `SemanticProperty`, and made class `Relation` extend from `SemanticProperty`.
1. Collapsed the `Restriction` class hierarchy into 3 subclasses restricting range, cardinality, or value of a property, and made class `Entity` has a single collection for restrictions.
1. Removed class `LinkAssertion` (replaced by `PropertyValueAssertion`) and made class `NamedInstance` has a single collection of property value assertions.
1. Moved the types collection (now of concrete type `TypeAssertion`) from both classes `ConceptInstance` and `RelationInstance` into their superclass `NamedInstance`.
1. Made class `RelationEntityPredicate` a subclass of both `UniaryPredicate` and `BinaryPredicate`.
1. Removed the `Reference` class hierarchy entirely and merged it into the `Member` class hierarchy (a member can have either a name or a ref to another member).

#### Migration Guide

Migrating OML v1 textual syntax to v2 can be done using the following process:

1. Replace the symbol `:>` by `<`.

1. Replace the symbol `ci ` by `instance `.

1. Replace the symbol `ri ` by `relation instance `.

1. Replace the syntax `keyword NAME < REFS [Details]` by the syntax `keyword NAME [Details] < REFs`.

1. Replace the syntax `keyword NAME [Restrictions]` by the syntax `keyword NAME < [Restrictions]`.

1. Replace the syntax `keyword NAME < REFS [Details Restrictions]` by the syntax `keyword NAME [Details] < REFS [Restrictions]`.

1. Replace the syntax `concept NAME [ enumerates REFS ]` by the syntax `concept NAME [ oneOf REFS ]`.

1. Replace the syntax `enumerated scalar NAME < REFS [ LITERALS ]` by the syntax `scalar NAME [ oneOf LITERALS ]`.

1. Replace the syntax `scalar NAME < REFS [ FACETS ]` by the syntax `scalar NAME = REFS [ FACETS ]`.

Migrating OML v1 XMI syntax for Descriptions to v2 can be done using the following process:

1. Replace the expression `ConceptTypeAssertion` by `TypeAssertion`.

1. Replace the expression `ScalarPropertyValueAssertion` by `PropertyValueAssertion`.

1. Replace the expression `LinkAssertion` by `PropertyValueAssertion`.

1. Replace the expression `<value` by `<literalValue`.

1. Replace the expression `<ownedLinks` by `<ownedPropertyValues`.

1. Replace the expression `<relation` by `<property`.

1. Replace the expression `target=` by `namedInstanceValue=`.



Please provide your feedback to [Maged Elaasar](https://opencaesar.github.io/contributors/Maged%20Elaasar.html) directly, or by commenting on this community [discussion](https://github.com/opencaesar/community/discussions/2).